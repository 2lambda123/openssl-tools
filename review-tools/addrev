#!/usr/bin/perl

my $args = "";
my $filterargs = "HEAD^..";
my $list_reviewers = 0;
my $help = 0;
my $haveprnum = 0;
my $trivial = 0;

my $my_email;

foreach (@ARGV) {
    if (/^[a-z][-a-z]*$/) {
        $args .= "--reviewer=$_ ";
    } elsif (/^--reviewer=(.+)$/) {
        $args .= "--reviewer=$1 ";
    } elsif (/^--rmreviewers$/) {
        $args .= "--rmreviewers ";
    } elsif (/^--trivial$/) {
        $args .= "--trivial ";
    } elsif (/^--verbose$/) {
        $args .= "--verbose ";
    } elsif (/^--myemail=(.+)$/) {
        $my_email = $1;
    } elsif (/^--nopr$/) {
	$haveprnum = 1;
    } elsif (/^--prnum=(.+)$/) {
        $args .= "--prnum=$1 ";
	$haveprnum = 1;
    } elsif (/^--commit=(.+)$/) {
        $args .= "--commit=$1 ";
    } elsif (/^-(\d+)$/) {
        $filterargs = "HEAD~$1..";
    } elsif (/^--list$/) {
	$list_reviewers = 1;
	last;
    } elsif (/^--help$/ || /^-h$/) {
	$help = 1;
	last;
    } else {
        $filterargs = $_;
    }
}

if ($list_reviewers) {
    system("gitaddrev --list");
    exit(0);
}
if ($help) {
    usage();
    exit(0);
}

die "Need either --prnum or --nopr flag" unless $haveprnum;

if (!defined $my_email) {
    $my_email = `git config --get user.email`;
}

$args .= "--myemail=$my_email ";

system("git filter-branch -f --tag-name-filter cat --msg-filter \"gitaddrev $args\" $filterargs");

sub usage {
    print STDERR <<"EOF";
usage: addrev args...

option style arguments:

--help			Print this help and exit
--list			List the known reviewers and exit (discards all other
			arguments)
--verbose		Be a bit more verbose
--reviewer=<reviewer>	A reviewer to be added on a Reviewed-by: line
--rmreviewers		Remove all existing Reviewed-by: lines before adding
			reviewers
--commit=<id>		Only apply to commit <id>
--myemail=<email>	Set email address.  Defaults to the result from
			git configuration setting user.email.
--trivial		Add a "CLA: trivial" to the message.
--nopr			Do not requre a PR number.
--prnum=NNN             Add a reference to GitHub pull request NNN
-<n>			Change the last <n> commits.  Defaults to 1.

non-option style arguments can be:

a string of lower case letters, denoting a reviewer name.

anything else will be used as a commit range.

Examples (all meaning the same thing):

  addrev -2 steve levitte
  addrev steve levitte HEAD^^..
  addrev --reviewer=steve --reviewer=levitte -2
EOF
}
